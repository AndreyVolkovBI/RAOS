plugins {
    id 'net.researchgate.release' version '2.6.0' apply false

    id "org.jetbrains.kotlin.kapt" version "1.3.72" apply false
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
}

apply plugin: 'net.researchgate.release'
apply plugin: 'java'

ext {
    mavenRepo = "repo"
    mavenUser = "user"
    mavenPassword = "password"
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
        jcenter()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }

    dependencies {
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
        implementation group: 'org.web3j', name: 'core', version: '5.0.0'

        implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
        implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.9.8'
    }
}

afterReleaseBuild.dependsOn(':raos:publish')

configure(subprojects.findAll {
    it.name == 'raos'
}) {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    release {
        git {
            requireBranch = 'develop|master'
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    publishing {
        publications {
            mavenJava(MavenPublication) {
                version = rootProject.version
                artifactId = project.name
                groupId = "GROUP_ID"

                from components.java
                artifact sourceJar {
                    classifier 'sources'
                }
            }
        }
        repositories {
            maven {
                url mavenRepo
                credentials {
                    username = mavenUser
                    password = mavenPassword
                }
            }
        }
    }
}
